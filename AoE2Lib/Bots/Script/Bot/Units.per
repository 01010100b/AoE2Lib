; sn 401-404
; goals 151-390

(defconst gl-units-command gl-temp1)
(defconst gl-units-player gl-temp2)
(defconst gl-units-x gl-temp3)
(defconst gl-units-y gl-temp4)
(defconst gl-units-radius gl-temp5)
(defconst gl-units-type gl-temp6)
(defconst gl-units-func-pointer gl-temp7)
(defconst gl-units-return-addr gl-temp8)
(defconst gl-units-data-pointer gl-temp9)
(defconst gl-units-temp gl-temp10)

(defconst gl-units-unit-id gl-temp11)
(defconst gl-units-unit-x gl-temp12)
(defconst gl-units-unit-y gl-temp13)
(defconst gl-units-unit-type gl-temp14)
(defconst gl-units-unit-player gl-temp15)
(defconst gl-units-unit-hp gl-temp16)
(defconst gl-units-unit-order gl-temp17)
(defconst gl-units-unit-nextattack gl-temp18)
(defconst gl-units-unit-stance gl-temp19)
(defconst gl-units-current-index gl-temp20)
(defconst gl-units-unit-target gl-temp21)

(defconst TYPE_MILITARY 0)
(defconst TYPE_CIVILIAN 1)
(defconst TYPE_BUILDING 2)
(defconst TYPE_WOOD 3)
(defconst TYPE_FOOD 4)
(defconst TYPE_GOLD 5)
(defconst TYPE_STONE 6)
(defconst TYPE_ALL 7)

; reset data

(defrule
	(true)
=>
	(set-goal gl-units-data-pointer 151)
	(up-modify-goal gl-units-temp g:= gl-units-data-pointer)
)

(defrule
	(up-compare-goal gl-units-temp c:<= 390)
=>
	(up-set-indirect-goal g: gl-units-temp c: -1)
	(up-modify-goal gl-units-temp c:+ 1)
	(up-jump-rule -1)
)

;region unit function

(defrule
	(true)
=>
	(up-get-rule-id gl-units-func-pointer)
	(up-modify-goal gl-units-func-pointer c:+ 1)
	(up-jump-rule 21)
)

;unpack command
(defrule
	(up-compare-goal gl-units-command c:>= 0)
=>
	(up-modify-goal gl-units-temp g:= gl-units-command)
	
	(up-modify-goal gl-units-type g:= gl-units-temp)
	(up-modify-goal gl-units-type c:mod 8)
	(up-modify-goal gl-units-temp c:z/ 8)
	
	(up-modify-goal gl-units-radius g:= gl-units-temp)
	(up-modify-goal gl-units-radius c:mod 100)
	(up-modify-goal gl-units-temp c:z/ 100)
	
	(up-modify-goal gl-units-y g:= gl-units-temp)
	(up-modify-goal gl-units-y c:mod 500)
	(up-modify-goal gl-units-temp c:z/ 500)
	
	(up-modify-goal gl-units-x g:= gl-units-temp)
	(up-modify-goal gl-units-x c:mod 500)
	(up-modify-goal gl-units-temp c:z/ 500)
	
	(up-modify-goal gl-units-player g:= gl-units-temp)
	(up-modify-goal gl-units-player c:mod 7)
)

;set search state
(defrule
	(up-compare-goal gl-units-command c:>= 0)
=>
	(up-modify-goal gl-point1-x g:= gl-units-x)
	(up-modify-goal gl-point1-y g:= gl-units-y)
	(up-set-target-point gl-point1)
	(up-modify-sn sn-focus-player-number g:= gl-units-player)
	(up-full-reset-search)
)

; military
(defrule
	(up-compare-goal gl-units-command c:>= 0)
	(up-compare-goal gl-units-type c:== TYPE_MILITARY)
=>
	(up-filter-distance c: -1 g: gl-units-radius)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-remote c: -1 c: 35)
	(up-filter-include cmdid-monk -1 -1 -1)
	(up-find-remote c: -1 c: 5)
	
	(up-reset-filters)
	(up-filter-include cmdid-monk -1 -1 -1)
	(up-find-remote c: -1 c: 5)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-remote c: -1 c: 40)
)

; civilian
(defrule
	(up-compare-goal gl-units-command c:>= 0)
	(up-compare-goal gl-units-type c:== TYPE_CIVILIAN)
=>
	(up-filter-distance c: -1 g: gl-units-radius)
	(up-filter-include cmdid-villager -1 -1 -1)
	(up-find-remote c: -1 c: 30)
	(up-filter-include cmdid-trade -1 -1 -1)
	(up-find-remote c: -1 c: 5)
	(up-filter-include cmdid-fishing-ship -1 -1 -1)
	(up-find-remote c: -1 c: 5)
)
(defrule
	(up-compare-goal gl-units-command c:>= 0)
	(up-compare-goal gl-units-type c:== TYPE_CIVILIAN)
=>
	(up-reset-filters)
	(up-filter-include cmdid-fishing-ship -1 -1 -1)
	(up-find-remote c: -1 c: 5)
	(up-filter-include cmdid-trade -1 -1 -1)
	(up-find-remote c: -1 c: 5)
	(up-filter-include cmdid-villager -1 -1 -1)
	(up-find-remote c: -1 c: 40)
)

; buildings
(defrule
	(up-compare-goal gl-units-command c:>= 0)
	(up-compare-goal gl-units-type c:== TYPE_BUILDING)
=>
	(up-filter-distance c: -1 g: gl-units-radius)
	(up-filter-include cmdid-military-building -1 -1 -1)
	(up-find-remote c: -1 c: 20)
	(up-filter-include cmdid-civilian-building -1 -1 -1)
	(up-find-remote c: -1 c: 20)
	
	(up-reset-filters)
	(up-filter-include cmdid-civilian-building -1 -1 -1)
	(up-find-remote c: -1 c: 20)
	(up-filter-include cmdid-military-building -1 -1 -1)
	(up-find-remote c: -1 c: 40)
)

; wood
(defrule
	(up-compare-goal gl-units-command c:>= 0)
	(up-compare-goal gl-units-type c:== TYPE_WOOD)
=>
	(up-filter-distance c: -1 g: gl-units-radius)
	(up-filter-status c: status-ready c: list-active)
	(up-find-resource c: wood c: 30)
	(up-filter-status c: status-resource c: list-active)
	(up-find-resource c: wood c: 10)
	
	(up-reset-filters)
	(up-filter-status c: status-resource c: list-active)
	(up-find-resource c: wood c: 10)
	(up-filter-status c: status-ready c: list-active)
	(up-find-resource c: wood c: 40)
)

; food
(defrule
	(up-compare-goal gl-units-command c:>= 0)
	(up-compare-goal gl-units-type c:== TYPE_FOOD)
=>
	(up-filter-distance c: -1 g: gl-units-radius)
	(up-filter-status c: status-ready c: list-active)
	(up-find-resource c: food c: 30)
	(up-filter-status c: status-resource c: list-active)
	(up-find-resource c: food c: 10)
	
	(up-reset-filters)
	(up-filter-status c: status-resource c: list-active)
	(up-find-resource c: food c: 10)
	(up-filter-status c: status-ready c: list-active)
	(up-find-resource c: food c: 40)
)

; gold
(defrule
	(up-compare-goal gl-units-command c:>= 0)
	(up-compare-goal gl-units-type c:== TYPE_GOLD)
=>
	(up-filter-distance c: -1 g: gl-units-radius)
	(up-filter-status c: status-resource c: list-active)
	(up-find-resource c: gold c: 40)
	
	(up-reset-filters)
	(up-filter-status c: status-resource c: list-active)
	(up-find-resource c: gold c: 40)
)

; stone
(defrule
	(up-compare-goal gl-units-command c:>= 0)
	(up-compare-goal gl-units-type c:== TYPE_STONE)
=>
	(up-filter-distance c: -1 g: gl-units-radius)
	(up-filter-status c: status-resource c: list-active)
	(up-find-resource c: stone c: 40)
	
	(up-reset-filters)
	(up-filter-status c: status-resource c: list-active)
	(up-find-resource c: stone c: 40)
)

; all (excluding trees)
(defrule
	(up-compare-goal gl-units-command c:>= 0)
	(up-compare-goal gl-units-type c:== TYPE_ALL)
=>
	(up-filter-distance c: -1 g: gl-units-radius)
	(up-filter-exclude -1 -1 -1 915)
	(up-find-remote c: -1 c: 40)
	
	(up-reset-filters)
	(up-filter-exclude -1 -1 -1 915)
	(up-find-remote c: -1 c: 40)
)

; clean and sort
(defrule
	(up-compare-goal gl-units-command c:>= 0)
=>
	(up-clean-search search-remote -1 search-order-asc)
	(up-clean-search search-remote object-data-distance search-order-asc)
	(up-remove-objects search-remote object-data-index c:>= 20)
	(up-get-search-state gl-search-state)
	(set-goal gl-units-current-index 0)
)

; write out data
(defrule
	(up-compare-goal gl-units-command c:>= 0)
	(up-compare-goal gl-units-current-index g:< gl-remote-total)
=>
	(up-set-target-object search-remote g: gl-units-current-index)
	(up-get-object-data object-data-id gl-units-unit-id)
	(up-get-object-data object-data-point-x gl-units-unit-x)
	(up-get-object-data object-data-point-y gl-units-unit-y)
	(up-get-object-data object-data-upgrade-type gl-units-unit-type)
	(up-get-object-data object-data-player gl-units-unit-player)
	(up-get-object-data object-data-hitpoints gl-units-unit-hp)
	(up-get-object-data object-data-order gl-units-unit-order)
	(up-get-object-data object-data-next-attack gl-units-unit-nextattack)
	(up-get-object-data object-data-attack-stance gl-units-unit-stance)
	(up-get-object-data object-data-target-id gl-units-unit-target)
)

; goal0
(defrule
	(up-compare-goal gl-units-command c:>= 0)
	(up-compare-goal gl-units-current-index g:< gl-remote-total)
=>
	(up-modify-goal gl-units-unit-target c:max 0)
	(up-modify-goal gl-units-unit-target c:min 44999)
	(up-modify-goal gl-units-temp g:= gl-units-unit-target)
	
	(up-modify-goal gl-units-temp c:* 45000)
	(up-modify-goal gl-units-unit-id c:max 0)
	(up-modify-goal gl-units-unit-id c:min 44999)
	(up-modify-goal gl-units-temp g:+ gl-units-unit-id)
	
	(up-set-indirect-goal g: gl-units-data-pointer g: gl-units-temp)
	(up-modify-goal gl-units-data-pointer c:+ 1)
)

; goal1
(defrule
	(up-compare-goal gl-units-command c:>= 0)
	(up-compare-goal gl-units-current-index g:< gl-remote-total)
=>
	(up-modify-goal gl-units-unit-type c:max 0)
	(up-modify-goal gl-units-unit-type c:min 1999)
	(up-modify-goal gl-units-temp g:+ gl-units-unit-type)
	
	(up-modify-goal gl-units-temp c:* 500)
	(up-modify-goal gl-units-unit-y c:max 0)
	(up-modify-goal gl-units-unit-y c:min 499)
	(up-modify-goal gl-units-temp g:+ gl-units-unit-y)
	
	(up-modify-goal gl-units-temp c:* 500)
	(up-modify-goal gl-units-unit-x c:max 0)
	(up-modify-goal gl-units-unit-x c:min 499)
	(up-modify-goal gl-units-temp g:= gl-units-unit-x)
	
	(up-set-indirect-goal g: gl-units-data-pointer g: gl-units-temp)
	(up-modify-goal gl-units-data-pointer c:+ 1)
)

; goal2
(defrule
	(up-compare-goal gl-units-command c:>= 0)
	(up-compare-goal gl-units-current-index g:< gl-remote-total)
=>
	(up-modify-goal gl-units-unit-stance c:max 0)
	(up-modify-goal gl-units-unit-stance c:min 3)
	(up-modify-goal gl-units-temp g:= gl-units-unit-stance)
	
	(up-modify-goal gl-units-temp c:* 20)
	(up-modify-goal gl-units-unit-nextattack c:/ 200)
	(up-modify-goal gl-units-unit-nextattack c:max 0)
	(up-modify-goal gl-units-unit-nextattack c:min 19)
	(up-modify-goal gl-units-temp g:+ gl-units-unit-nextattack)
	
	(up-modify-goal gl-units-temp c:* 40)
	(up-modify-goal gl-units-unit-order c:- 700)
	(up-modify-goal gl-units-unit-order c:max -1)
	(up-modify-goal gl-units-unit-order c:min 38)
	(up-modify-goal gl-units-unit-order c:+ 1)
	(up-modify-goal gl-units-temp g:+ gl-units-unit-order)
)
(defrule
	(up-compare-goal gl-units-command c:>= 0)
	(up-compare-goal gl-units-current-index g:< gl-remote-total)
=>
	(up-modify-goal gl-units-temp c:* 1000)
	(up-modify-goal gl-units-unit-hp c:max 0)
	(up-modify-goal gl-units-unit-hp c:min 999)
	(up-modify-goal gl-units-temp g:+ gl-units-unit-hp)
	
	(up-modify-goal gl-units-temp c:* 10)
	(up-modify-goal gl-units-unit-player c:max 0)
	(up-modify-goal gl-units-unit-player c:min 9)
	(up-modify-goal gl-units-temp g:+ gl-units-unit-player)
	
	(up-set-indirect-goal g: gl-units-data-pointer g: gl-units-temp)
	(up-modify-goal gl-units-data-pointer c:+ 1)
	
	(up-modify-goal gl-units-current-index c:+ 1)
)

; loop
(defrule
	(up-compare-goal gl-units-command c:>= 0)
	(up-compare-goal gl-units-current-index g:< gl-remote-total)
=>
	(up-jump-rule -6)
)

(defrule
	(up-compare-goal gl-units-command c:>= 0)
	(up-compare-goal gl-units-current-index c:< 20)
=>
	(up-modify-goal gl-units-data-pointer c:+ 3)
	(up-modify-goal gl-units-current-index c:+ 1)
	(up-jump-rule -1)
)

(defrule
	(up-compare-goal gl-units-command c:< 0)
=>
	(up-modify-goal gl-units-data-pointer c:+ 60)
)

(defrule
	(true)
=>
	(up-jump-direct g: gl-units-return-addr)
)

;endregion

; get commands

(defrule
	(true)
=>
	(up-modify-goal gl-units-command s:= 401)
	(up-get-rule-id gl-units-return-addr)
	(up-modify-goal gl-units-return-addr c:+ 1)
	(up-jump-direct g: gl-units-func-pointer)
)

(defrule
	(true)
=>
	(up-modify-goal gl-units-command s:= 402)
	(up-get-rule-id gl-units-return-addr)
	(up-modify-goal gl-units-return-addr c:+ 1)
	(up-jump-direct g: gl-units-func-pointer)
)

(defrule
	(true)
=>
	(up-modify-goal gl-units-command s:= 403)
	(up-get-rule-id gl-units-return-addr)
	(up-modify-goal gl-units-return-addr c:+ 1)
	(up-jump-direct g: gl-units-func-pointer)
)

(defrule
	(true)
=>
	(up-modify-goal gl-units-command s:= 404)
	(up-get-rule-id gl-units-return-addr)
	(up-modify-goal gl-units-return-addr c:+ 1)
	(up-jump-direct g: gl-units-func-pointer)
)