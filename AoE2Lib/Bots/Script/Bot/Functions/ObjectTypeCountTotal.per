;func ObjectTypeCountTotal
; param1 = type id

(defconst glx-objecttypecounttotal-type gl-func-param1)
(defconst glx-objecttypecounttotal-min gl-func-reg1)
(defconst glx-objecttypecounttotal-max gl-func-reg2)
(defconst glx-objecttypecounttotal-mid gl-func-reg3)

(defrule
	(true)
=>
	(up-get-rule-id gl-func-pointer-objecttypecounttotal)
	(up-modify-goal gl-func-pointer-objecttypecounttotal c:+ 1)
	(up-jump-rule 7)
)

(defrule
	(up-object-type-count-total g: glx-objecttypecounttotal-type c:<= 0)
=>
	(set-goal gl-func-result 0)
	(up-jump-direct g: gl-func-return-addr)
)

(defrule
	(up-object-type-count-total g: glx-objecttypecounttotal-type c:>= 1024)
=>
	(set-goal gl-func-result 1024)
	(up-jump-direct g: gl-func-return-addr)
)



(defrule
	(true)
=>
	(set-goal glx-objecttypecounttotal-min 0)
	(set-goal glx-objecttypecounttotal-max 1024)
	(up-modify-goal glx-objecttypecounttotal-mid g:= glx-objecttypecounttotal-min)
	(up-modify-goal glx-objecttypecounttotal-mid g:+ glx-objecttypecounttotal-max)
	(up-modify-goal glx-objecttypecounttotal-mid c:z/ 2)
)

(defrule
	(up-compare-goal glx-objecttypecounttotal-mid g:!= glx-objecttypecounttotal-min)
	(up-compare-goal glx-objecttypecounttotal-mid g:!= glx-objecttypecounttotal-max)
	(up-object-type-count-total g: glx-objecttypecounttotal-type g:< glx-objecttypecounttotal-mid)
=>
	(up-modify-goal glx-objecttypecounttotal-max g:= glx-objecttypecounttotal-mid)
	(up-modify-goal glx-objecttypecounttotal-mid g:= glx-objecttypecounttotal-min)
	(up-modify-goal glx-objecttypecounttotal-mid g:+ glx-objecttypecounttotal-max)
	(up-modify-goal glx-objecttypecounttotal-mid c:z/ 2)
	(up-jump-rule -1)
)

(defrule
	(up-compare-goal glx-objecttypecounttotal-mid g:!= glx-objecttypecounttotal-min)
	(up-compare-goal glx-objecttypecounttotal-mid g:!= glx-objecttypecounttotal-max)
	(up-object-type-count-total g: glx-objecttypecounttotal-type g:>= glx-objecttypecounttotal-mid)
=>
	(up-modify-goal glx-objecttypecounttotal-min g:= glx-objecttypecounttotal-mid)
	(up-modify-goal glx-objecttypecounttotal-mid g:= glx-objecttypecounttotal-min)
	(up-modify-goal glx-objecttypecounttotal-mid g:+ glx-objecttypecounttotal-max)
	(up-modify-goal glx-objecttypecounttotal-mid c:z/ 2)
	(up-jump-rule -2)
)

(defrule
	(up-object-type-count-total g: glx-objecttypecounttotal-type g:>= glx-objecttypecounttotal-max)
=>
	(up-modify-goal gl-func-result g:= glx-objecttypecounttotal-max)
	(up-jump-direct g: gl-func-return-addr)
)

(defrule
	(up-object-type-count-total g: glx-objecttypecounttotal-type g:<= glx-objecttypecounttotal-min)
=>
	(up-modify-goal gl-func-result g:= glx-objecttypecounttotal-min)
	(up-jump-direct g: gl-func-return-addr)
)